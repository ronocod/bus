apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

Properties properties = new Properties()
properties.load(project.rootProject.file('user.properties').newDataInputStream())
def googleMapsApiKey = properties.getProperty('googleMapsApiKey')
def storeFilePath = properties.getProperty('storeFile')

android {
  compileSdkVersion 28

  if (storeFilePath) {
    signingConfigs {
      release {
        storeFile file(storeFilePath)
        storePassword properties.getProperty('storePassword')
        keyAlias properties.getProperty('keyAlias')
        keyPassword properties.getProperty('keyPassword')
      }
    }
  }

  defaultConfig {
    applicationId "com.conorodonnell.bus"
    minSdkVersion 21
    targetSdkVersion 28
    versionCode 8
    versionName "1.4."
    vectorDrawables.useSupportLibrary = true
    manifestPlaceholders = [mapsApiKey: "$googleMapsApiKey"]
  }
  buildTypes {
    debug {
      applicationIdSuffix '.debug'
      versionNameSuffix '-dev'
    }
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      versionNameSuffix new Date().format('yyyy-MM-dd-HH-mm')

      if (storeFilePath) {
        signingConfig signingConfigs.release
      }
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
  implementation "androidx.legacy:legacy-support-v4:1.0.0"
  implementation "androidx.appcompat:appcompat:1.1.0"
  implementation "com.google.android.material:material:1.0.0"
  implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
  implementation "androidx.vectordrawable:vectordrawable:1.1.0"
  implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
  implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"
  implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofit"
  implementation "com.squareup.moshi:moshi:1.8.0"
  implementation 'com.squareup.okhttp3:okhttp:4.2.2'
  implementation 'com.squareup.okhttp3:logging-interceptor:4.2.2'

  implementation "io.reactivex.rxjava2:rxjava:2.2.13"
  implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
  implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

  implementation "androidx.room:room-runtime:$versions.room"
  implementation "androidx.room:room-rxjava2:$versions.room"
  implementation "com.google.android.gms:play-services-maps:17.0.0"
  implementation "com.google.android.gms:play-services-location:17.0.0"

  implementation 'net.sharewire:google-maps-clustering:0.1.3'

  kapt "androidx.room:room-compiler:$versions.room"
  kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.8.0'

  testImplementation 'junit:junit:4.12'
}

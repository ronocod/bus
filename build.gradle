buildscript {
  ext.versions = [
      room          : "1.1.1",
      supportLibrary: '28.0.0',
      kotlin        : '1.3.31',
      retrofit      : '2.5.0',
  ]
  repositories {
    google()
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.4.1'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
  }
}

plugins {
  id "com.github.ben-manes.versions" version "0.21.0"
}

allprojects {
  repositories {
    google()
    jcenter()
  }
}

subprojects {
  configurations {
    ktlint
  }

  dependencies {
    ktlint "com.github.shyiko:ktlint:0.31.0"
  }

  task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml", "src/**/*.kt"
  }

  task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}
